

package rs.ac.bg.etf.pp1;

import java_cup.runtime.*;
import org.apache.log4j.*;
import rs.ac.bg.etf.pp1.ast.*;

parser code {:

	boolean errorDetected;

	Logger log = Logger.getLogger(getClass());


    // slede redefinisani metodi za prijavu gresaka radi izmene teksta poruke

    public void report_fatal_error(String message, Object info) throws java.lang.Exception {
      done_parsing();
      report_error(message, info);
      errorDetected = true;
    }

    public void syntax_error(Symbol cur_token) {
        report_error("\nSintaksna greska", cur_token);
        errorDetected = true;
    }

    public void unrecovered_syntax_error(Symbol cur_token) throws java.lang.Exception {
        report_fatal_error("Fatalna greska, parsiranje se ne moze nastaviti", cur_token);
        errorDetected = true;
    }

    public void report_error(String message, Object info) {
    	errorDetected = true;
    	StringBuilder msg = new StringBuilder(message);
    	if (info instanceof Symbol)
            msg.append (" na liniji ").append(((Symbol)info).left);
        log.error(msg.toString());
    }


:}

init with {:
	errorDetected = false;
:}

scan with {:
	Symbol s = this.getScanner().next_token();
	if (s != null && s.value != null)
		log.info(s.toString() + " " + s.value.toString());
	return s;
:}

terminal PROG, RETURN, RBRACE, LBRACE, CONST;
terminal EQUAL, PLUSPLUS, MINUSMINUS, DOTCOMMA, READ, LPAREN, RPAREN, PRINT, COMMA;
terminal NEW, LSQUARE, RSQUARE, PLUS, MINUS, MUL, DIV, PERCENT;
terminal String IDENT;
terminal Integer NUMBER;
terminal Boolean BOOLCONST;
terminal Character CHARCONST;

nonterminal DesignatorStatement, NumberInPrint, Minus, Addop, FunctionParameters, Lsqare, Rsqare;
nonterminal MulopFactor, Mulop, ExprList, VarDecl, SquareList, IdentInLine, IdentList, OptionalComma;
nonterminal IdentSquareList, FormPars, TypeIdentSquareList, DesignatorListMany, DesignatorList;
nonterminal VarDeclList, MethodDeclList, StatementList, IdentInLineList, OneVarDeclaration, OneVarDeclarationList;

nonterminal rs.etf.pp1.symboltable.concepts.Obj ProgName, Program, MethodDecl, MethodTypeName, Designator, FormParsLst, IdentTypeInLine, ReturnStatement;
nonterminal rs.etf.pp1.symboltable.concepts.Struct Type, Term, Factor, Expr, AddopTerm, Statement, Constant;

Program ::= (Program) PROG ProgName:p VarDeclList LBRACE MethodDeclList RBRACE;

ProgName ::= (ProgName) IDENT:progName;

VarDeclList ::= (VarDeclarations) VarDeclList VarDecl
			|
			(NoVarDecl) /* epsilon smena */
			;

VarDecl ::= (VarDeclarationOther) Type:varType OneVarDeclarationList DOTCOMMA
          | (ConstVarDeclaration) CONST Type:varType IDENT:varName EQUAL Constant:f DOTCOMMA
          |
            error DOTCOMMA:l
            {: parser.report_error("Izvrsen oporavak do ; u liniji " + lleft, null); :}
          ;

OneVarDeclaration ::= (VarDeclarationArray) IDENT:varName LSQUARE RSQUARE
                    | (VarDeclarationMatrix) IDENT:varName LSQUARE RSQUARE LSQUARE RSQUARE
                    | (VarDeclaration) IDENT:varName
                    |
                        error COMMA:l
                        {: parser.report_error("Izvrsen oporavak do , u liniji " + lleft, null); :}
                    ;

OneVarDeclarationList ::= (VarDeclarationList) OneVarDeclarationList OptionalComma OneVarDeclaration
                        | (NoVarDeclarationList) /* epsilone smena */
                        ;

OptionalComma ::= (OptionalCommaClass) COMMA
                | (NoOptionalCommaClass) /* epsilon smena */
                ;

MethodDeclList ::= (MethodDeclarations) MethodDeclList MethodDecl
				|
				(NoMethodDecl) /* epsilon smena */
				;

MethodDecl ::= (MethodDecl) MethodTypeName LPAREN FormParsLst RPAREN VarDeclList LBRACE StatementList ReturnStatement RBRACE;

ReturnStatement ::= (ReturnStatementExpr) RETURN Expr:exprName DOTCOMMA
                  | (NoReturnStatement) /* epsilon smena */
                  ;

MethodTypeName ::= (MethodTypeName) Type:retType IDENT:metName;

StatementList ::= (Statements) StatementList Statement
				|
				(NoStmt) /* epsilon smena */
				;

FormParsLst ::= (FormParams) FormParsLst Type:typeName IDENT:identName IdentList
		  | (NoFormParams) /* epsilon smena */
		  ;

IdentList ::= (IdentLists) IdentList IdentTypeInLine
            | (NoIdentLists) /* epsilon smena */
            ;

DesignatorStatement ::= (DesignatorStatementEqual) Designator:dest EQUAL Expr:e
                       | (DesignatorStatementPlusPlus) Designator PLUSPLUS
                       | (DesignatorStatementMinusMinus) Designator MINUSMINUS
                       ;

DesignatorListMany ::= (DesignatorListRec) DesignatorListMany COMMA DesignatorList
                     | (NoDesignatorList) /* epsilon smena */
                     ;

DesignatorList ::= (DesignatorListDesignator) Designator
                 | (NoDesignatorListDesignator) /* epsilon smena */
                 ;

Statement ::= (StatementDesignator) DesignatorStatement DOTCOMMA
            |
              error DOTCOMMA:l
              {: parser.report_error("Izvrsen oporavak do ; u liniji " + lleft, null); :}
            | (StatementRead) READ LPAREN Designator RPAREN DOTCOMMA
            | (PrintStmt) PRINT LPAREN Expr NumberInPrint RPAREN DOTCOMMA
            ;

NumberInPrint ::= (NumberInPrintNumber) COMMA NUMBER
                | (NoNumberInPrintNumber) /* epsilon smena */
                ;

Expr ::= (Expression) Minus Term:t AddopTerm:a ;

AddopTerm ::= (AddOperationTerminatorRec) AddopTerm:te Addop Term:t
            | (NoAddOperationTerminator) /* epsilon smena */
            ;

Minus ::= (MinusExpression) MINUS
        | (NoMinusExpression) /* epsilon smena */
        ;

Term ::= (Terminator) Factor:t MulopFactor ;

MulopFactor ::= (MulopFactorRec) MulopFactor Mulop Factor
              | (NoMulopFactor) /* epsilon smena */
              ;

Factor ::= (Consts) Constant:c
         | (FactorExpr) LPAREN Expr RPAREN
         | (FactorNewExpr) NEW Type:t Lsqare Expr:e Rsqare
         | (FactorDes) Designator:d
         | (FactorNewTypeExpr) NEW Type:t Lsqare Expr:e1 Rsqare Lsqare Expr:e2 Rsqare
         ;

Constant ::= (FactorNum) NUMBER:value
           | (FactorChar) CHARCONST:value
           | (FactorBool) BOOLCONST:value
           ;

Designator ::= (DesignatorExpression) IDENT:name ExprList ;

ExprList ::= (ExprListExpr) Lsqare Expr Rsqare
           | (ExprListExprMatrix) Lsqare Expr Rsqare Lsqare Expr Rsqare
           | (NoExprListExpr) /* epsilon smena */
           ;

Addop ::= (AddOperationPlus) PLUS
        | (AddOperationMinus) MINUS
        ;

Mulop ::= (MulOperationMul) MUL
        | (MulOperationDiv) DIV
        | (MulOperationPercent) PERCENT
        ;

IdentSquareList ::= (IdentSquareListRec) IdentSquareList IdentInLine SquareList
                  | (NoIdentSquareList) /* epsilon smena */
                  ;

IdentInLine ::= (IdentInLine) COMMA IDENT:identName;

IdentInLineList ::= (ListOfIdentInLine) IdentInLineList IdentInLine
                  | (IdentInLineList) /* epsilon smena */
                  ;

IdentTypeInLine ::= (IdentTypeInLine) COMMA Type:typeName IDENT:identName;

FormPars ::= (FormParsMatrix) Type IDENT SquareList TypeIdentSquareList DOTCOMMA;

TypeIdentSquareList ::= (TypeIdentSquareListRec) TypeIdentSquareList COMMA Type:typeName IDENT:identName SquareList
                  | (NoTypeIdentSquareList) /* epsilon smena */
                  ;

SquareList ::= (SquareListOutside) LSQUARE RSQUARE LSQUARE RSQUARE
             | (SquareListOutsideOne) LSQUARE RSQUARE
             | (NoSquareListOutside) /* epsilon smena */
             ;

Type ::= (TypeIdent) IDENT:typeName ;

Lsqare ::= (LsquareClass) LSQUARE;

Rsqare ::= (RsquareClass) RSQUARE;